\chapter{Introduction to Ada tasking}{``When Roman engineers built a
  bridge, they had to stand under it while the first legion marched
  across. If programmers today worked under similar ground rules, they
  might well find themselves getting much more interested in
  Ada.''}{Robert Dewar}
\label{chap:intro_ada}
Ada is a structured, statically (strongly) typed, imperative
object-oriented high-level computer programming language.  Ada is
fairly unusual in that its runtime provides extensive tasking
features. Traditionally tasking features are the province of operating
systems. But, factoring in tasking functionality to the language
runtime provides some advantages:

\begin{itemize}
\item{Uniform behaviour and API across operating systems}
\item{Greater reliability and control over tasking}
\item{Exploitation of inherent parallelism becomes easier and can be
  done at the compilation stage}
\end{itemize}

\section{Tasks}
In Ada, parallel activities are described by \emph{tasks}. Ada
procedures are different from C/C++ functions in that a procedure
itself may contain multiple, concurrent \emph{entry-points} in the
form of tasks, as well as other procedures/functions with local
scope. Code Listing~\ref{tasks_in_proc} shows this approach, where the
main procedure contains two different tasks itself. The output of this
code is completely random and depends only on the scheduler
implemented within the Ada runtime. Any of the six statements may be
executed in any order (the three~\texttt{Put} and the three
\texttt{New\_Line}) may be executed in any order.

Tasks inside procedures may also share data declared within the scope
of the procedure, which is intuitive and normal. But this also implies
that race conditions may occur if care is not taken to protect these
variables which are declared on the \emph{stack} of the thread. In
Listing~\ref{shared_data} both \texttt{Print\_A} and \texttt{Print\_B}
can access the variable \texttt{X} declared in the scope of the
procedure \texttt{Main}.

\begin{minipage}{0.45\linewidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Tasks in a procedure,label=tasks_in_proc]


procedure Main is
   task Print_A;
   task Print_B;

   task body Print_A is
   begin
      Put ("A");
      New_Line;
   end Print_A;

   task body Print_B is
   begin
      Put ("B");
      New_Line;
   end Print_B;

begin
	Put ("Hello world");
	New_Line;
end Main;


--
\end{lstlisting}
\end{minipage}
\hspace{5mm}
\begin{minipage}{0.45\linewidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Sharing of data in tasks,label=shared_data]
with Ada.Text_IO;
procedure Main is
   X : Integer := 0;

   task Print_A;
   task Print_B;

   task body Print_A is
   begin
      Put ("A printing X = ");
      Put (X);
      New_Line;
   end Print_A;

   task body Print_B is
   begin
      Put ("B printing X = ");
      Put (X);
      New_Line;
   end Print_B;

begin
   Put ("Hello world");
   New_Line;
end Main;
\end{lstlisting}
\end{minipage}

\section{Rendezvous}
In the previous examples the tasks did not interact directly with one
another once they were activated by the runtime, except for the fact
that their parent (the procedure \texttt{Main}) had to await their
termination in order to terminate itself. One way for tasks to
interact with one another would be by the sending of messages to one
another. In Ada, this form of IPC is called a \emph{rendezvous}. A
rendezvous occurs when one task calls (like a procedure) an
\emph{entry} declared in another task.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Task rendezvous,label=rendezvous_single]
with Ada.Text_IO;

procedure Main is
	use Ada.Text_IO;
	package I_IO is new Ada.Text_IO.Integer_IO (Integer);
	use I_IO;

	task Producer;
	task Consumer is
		entry Put_Element (Elem : Integer);
	end Consumer;

	task body Producer is
		X : Integer := 10;
		Param : Integer := 0;
	begin
		loop
			exit when X = 0;
			Consumer.Put_Element (Param);
			Param := Param + 1;
			X := X - 1;
		end loop;
	end Producer;

	task body Consumer is
	begin
		loop
			accept Put_Element (Elem : Integer) do
				Put ("Received element = ");
				Put (Elem);
				New_Line;
			end Put_Element;
		end loop;
	end Consumer;

begin
	Put ("Hello world");
	New_Line;
end Main;
\end{lstlisting}
\end{minipage}

In Listing~\ref{rendezvous_single} task \texttt{Producer} calls a
rendezvous \texttt{Put\_Element} exposed by task
\texttt{Consumer}. The semantic of this call is that whichever task
arrives first (be it the \texttt{Producer} that arrives at the call
first, or the \texttt{Consumer} that arrives at the accept first) will
wait for the other to arrive at the corresponding point in its thread
of execution before proceeding. Bear in mind that the \texttt{accept}
construct is a one-shot receiver for a rendezvous, which is why it is
enclosed in a loop that executes 10 times.

Rendezvous can easily produce deadlocks as is evident from
Listing~\ref{deadlock_simple}. The \texttt{Main} procedure calls the
entries \texttt{Compute\_Sum} and \texttt{Compute\_Product} in the
opposite order to their declaration inside the \texttt{Calculate}
task.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Deadlock in rendezvous caused by
    call/accept order inversion,label=deadlock_simple]
with Ada.Text_IO;

procedure Main is
	use Ada.Text_IO;
	package I_IO is new Ada.Text_IO.Integer_IO (Integer);
	use I_IO;

	task Calculator is
		entry Compute_Sum (A : Integer; B : Integer);
		entry Compute_Product (A : Integer; B : Integer);
	end Calculator;

	task body Calculator is
	begin
		accept Compute_Sum (A : Integer; B : Integer) do
			Put (A + B);
			New_Line;
		end Compute_Sum;

		accept Compute_Product (A:Integer; B:Integer) do
			Put (A * B);
			New_Line;
		end Compute_Product;
	end Calculator;

begin
	Calculator.Compute_Product (3, 4);
	Calculator.Compute_Sum (10, 20);
end Main;
\end{lstlisting}
\end{minipage}

If the order of calls is changed to
\texttt{Calculator.Compute\_Product (3, 4);} and
\texttt{Calculator.Compute\_Sum (10, 20);} then the program terminates
successfully. On the other hand, if an extra call to either of the two
entries is appended at the end of the \texttt{Main} procedure it
results in the runtime raising a \texttt{TASKING\_ERROR}
exception. This is because after accepting the two entries the
\texttt{Calculator} terminates and there is nothing to accept the call
from the procedure.

\section{Timing}
\label{ada_tasking_timing}
Ada provides two basic timing primitives, the \kw{delay}
\texttt{<Duration\_Expression>} to make a task sleep during a certain
quantum of time and the \kw{delay until} \texttt{<Time\_Expression>}
to make a task sleep until a certain instance of time. Since Ada has
tasking capabilities, it follows logically that the Ada runtime has a
scheduler. The Ada language core, however, does not say whether the
Ada scheduler is pre-emptive or not. It does stipulate that tasks may
have priorities, and that a higher priority task may not be
interrupted by a lower priority one. What the Ada semantics do say, is
that a statement of the form

\texttt{delay 2.5}\\will put the calling task in the state
\emph{suspend} for 2.5 seconds before being declared as \emph{ready}
again. Note that this does not mean that the task will be in the state
\emph{running} after the elapse of 2.5 seconds as in the meantime a
higher priority task may be running on the processor. In case the
processor is executing a lower priority task then the newly enabled
task will pre-empt it. This approach however, is not ideal since it
does not take into account timing drift due to the amount of time
required by various actions in the task. In order to overcome these
difficulties, Ada proposes the absolute timeout construct. The
statement

\kw{delay until} \texttt{Next\_Time;}\\ allows the calling task to be
suspended up until an absolute delay. Although the same risk
(inability to run because of a higher priority task occupying the
processor) as the earlier relative \texttt{delay} exists, yet there is
no chance of time drift creeping in between cycles of a real-time
system.

\section{Protected objects}
\emph{Protected objects} are high-level constructs used to ensure
mutual exclusion between concurrent tasks that need access to the same
data. Ada enforces the separation of the interface from the
implementation for protected objects. The public part of the protected
object's (or its type's) definition may not contain any data elements.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Declaring a protected object with
    procedures,label=po_procs]
protected PO is
	procedure Read (X : out Integer);
	procedure Write (X : in Integer);
private
	Data : Integer := 0;
end PO;

protected body PO is
	procedure Read (X : out Integer) is
	begin
		X := Data;
	end Read;

	procedure Write (X : in Integer) is
	begin
		Data := X;
	end write;
end PO;
\end{lstlisting}
\end{minipage}

Ada declares that a \emph{procedure} of a protected object may access
the private data part in an arbitrary manner, whereas a
\emph{function} may only read the private part of a (protected)
object. A procedure may not return a value, whereas a function can
return a value; although a procedure may, in effect, return a value
indirectly using an \emph{out} parameter. Thus many different tasks
can be inside a function (if none is inside any of the procedures) but
only a single task at a time can be a in a procedure (thus precluding
any others from entering any functions as well). This approach, in
essence, gives a high-level construct for implementing a CREW
(Concurrent Read Exclusive Write) protocol.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Same protected object but with a function
    for improved parallelism,label=po_proc_and_func]
protected PO is
	function Read return Integer;
	procedure Write (X : in Integer);
private
	Data : Integer := 0;
end PO;
\end{lstlisting}
\end{minipage}

The different functional entry-points of a protected object may also
be protected by \emph{guards}. This would be similar to having a
conditional statement at the beginning of the procedure or function
that exits without carrying out any treatment in case a guard
condition is false. Ada goes one step further on this concept by
introducing the concept of blocking guards on the entry-points into a
protected object. In Ada these guards are called \emph{barriers} and
in order to use them we use an \emph{entry} into the protected object
in place of a procedure or function.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Pipe IPC construct for two multiple tasks
    with a buffer size of 1,label=prod_consumer]
protected Pipe is
	procedure Get (X : out Integer);
	entry Put (X : in Integer);
private
	Transit_Data : Integer := 0;
	Transit_Ready : Boolean := true;
end Pipe;

protected body Pipe is
	procedure Get (X : out Integer) is
	begin
		X := Transit_Data;		
		Transit_Ready := True;
	end Get;

	entry Put (X : in Integer) when Transit_Ready is
	begin
		Transit_Data := X;
		Transit_Ready := False;
	end Put;
end Pipe;
\end{lstlisting}
\end{minipage}

As shown in Listing~\ref{prod_consumer}, the entry \texttt{Put} has a
barrier placed on it (the Boolean \texttt{Transit\_Ready}) which is
set to true when a recipient reads the stored value and false when a
sender puts a new value.

\section{Select statement}
The \texttt{select} statement allows the selection of one entry-point
among many offered by a task based on the request queued at the task's
buffer. It may be thought of as a C/C++ \texttt{switch/case} statement
but for task entry-points. Whereas a series of entries
(\texttt{accept} statements) in a task would only be invocable
sequentially (resulting in a deadlock if the caller is out of step
with the callee), a select statement would let the task choose between
any of the entries available based on the request available.

\begin{minipage}{\listingwidth}
\lstset{language=ada}
\begin{lstlisting}[caption=Protected variable implemented as a task
    with select entries,label=select_po]
task Protected_Variable is
	entry Read (X : out Integer);
	entry Write (X : in Integer);
end Protected_Variable;

task body Protected_Variable is
	Data : Integer := 0;		
begin
	loop
		select
			accept Read (X : out Integer) do
				X := Data;
			end;
		or
			accept Write (X : in Integer) do
				Data := X;
			end;
		end select;
	end loop;
end Protected_Variable;
\end{lstlisting}
\end{minipage}

In Listing~\ref{select_po}, the object \texttt{Protected\_Variable}
may respond to any combination of successive \texttt{Read} or
\texttt{Write} invocations because of the select construct.

